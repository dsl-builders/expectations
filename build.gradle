/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2024-2025 Vladimir Orany.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.java-project'
    id 'org.kordamp.gradle.checkstyle'
    id 'org.kordamp.gradle.codenarc'
    id 'org.kordamp.gradle.coveralls'
}

if (!project.hasProperty('mavenCentralUsername'))       ext.mavenCentralUsername            = System.getenv('MAVEN_CENTRAL_USERNAME') ?: '**UNDEFINED**'
if (!project.hasProperty('mavenCentralPassword'))       ext.mavenCentralPassword            = System.getenv('MAVEN_CENTRAL_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signing.keyId'))              ext['signing.keyId']                = System.getenv('SIGNING_KEY_ID') ?: '**UNDEFINED**'
if (!project.hasProperty('signing.password'))           ext['signing.password']             = System.getenv('SIGNING_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signing.secretKeyRingFile'))  ext['signing.secretKeyRingFile']    = System.getenv('SIGNING_SECRET_KEY_RING_FILE') ?: '**UNDEFINED**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Expectations DSL Builder'
        vendor      = 'Vladimir Orany'
        description = 'DSL for Writing JUnit Parametrized Tests'

        links {
            website      = "https://github.com/${slug}"
            issueTracker = "https://github.com/${slug}/issues"
            scm          = "https://github.com/${slug}.git"
        }

        people {
            person {
                id    = 'musketyr'
                name  = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        enabled = false
        signing {
            enabled = false
        }
    }

    artifacts {
        source {
            // handled by maven central publish plugin
            enabled = false
        }
    }

    quality {
        checkstyle {
            toolVersion = '8.27'
        }

        codenarc {
            toolVersion = '1.5'
        }
    }

    docs {
        javadoc {
            // handled by maven central publish plugin
            enabled = false
            autoLinks {
                enabled = false
            }
            aggregate {
                enabled = false
            }
        }
        groovydoc {
            enabled = false
            aggregate {
                enabled = false
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    license {
        exclude '**/*.json'
        exclude '***.yml'
    }
}

subprojects { subproject ->
    if (subproject.name == 'guide') return

    apply plugin: 'java-library'
    apply plugin: 'com.vanniktech.maven.publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        manifest.attributes provider: 'gradle'
    }

    mavenPublishing {
        publishToMavenCentral(true)
        signAllPublications()

        pom {
            name = "Expectations DSL Builder"
            description = 'DSL for Writing JUnit Parametrized Tests'
            inceptionYear = "2024"
            url = "https://github.com/${slug}"
            licenses {
                license {
                    name = "The Apache License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
            developers {
                developer {
                    id = "musketyr"
                    name = "Vladimir Orany"
                    url = "https://github.com/musketyr/"
                }
            }
            scm {
                url = "https://github.com/${slug}.git"
                connection = "scm:git:git://github.com/${slug}.git"
                developerConnection = "scm:git:ssh://git@github.com/${slug}.git"
            }
        }

    }
}
